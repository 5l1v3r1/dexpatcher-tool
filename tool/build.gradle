/*
 * DexPatcher - Copyright 2015, 2016 Rodrigo Balerdi
 * (GNU General Public License version 3 or later)
 *
 * DexPatcher is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published
 * by the Free Software Foundation, either version 3 of the License,
 * or (at your option) any later version.
 */

plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "1.2.3"
}

group = parent.group
version = parent.version

repositories {
    jcenter()

    // Use local fork of dexlib2:
    // (https://github.com/DexPatcher/dexlib2-fork-for-dexpatcher)
    //flatDir dirs: '../../dexlib2-fork-for-dexpatcher/dexlib2/build/libs'
    //flatDir dirs: '../../dexlib2-fork-for-dexpatcher/util/build/libs'
}

dependencies {
    compile 'org.smali:dexlib2:2.2b3'
    compile 'commons-cli:commons-cli:1.2'

    // Use local fork of dexlib2:
    //compile 'org.smali:dexlib2:2.1.3-dexpatcher'
    //compile 'org.smali:util:2.1.3-dexpatcher'
    //compile 'com.google.guava:guava:18.0'
}

compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

task writeVersionFile {
    ext.version = version
    ext.outputFile = file('build/classes/main/lanchon/dexpatcher/version');
    dependsOn compileJava
    inputs.property 'version', { ext.version }
    outputs.file { ext.outputFile }
    doLast {
        ext.outputFile.text = "${ext.version}"
    }
}
jar.dependsOn writeVersionFile

jar {
    baseName = 'dexpatcher-tool'
    manifest {
        attributes(
                'Implementation-Title': 'DexPatcher-tool',
                'Implementation-Version': version,
                'Main-Class': 'lanchon.dexpatcher.Main'
        )
    }
}

task shadowBugWorkaround(type: Jar) {
    dependsOn jar
    destinationDir file('build/shadow-bug-workaround')
    baseName = 'dexpatcher-annotation-nested'
    from jar
    from tasks.getByPath(':annotation:jar')
}

shadowJar {
    dependsOn jar
    baseName = 'dexpatcher'
    classifier = null
    exclude 'META-INF/**'
    from 'extras'
    // Shadow plugin bug: explodes the nested jar.
    //from tasks.getByPath(':annotation:jar')
    // Workaround: double-nest the jar.
    from shadowBugWorkaround
}
assemble.dependsOn shadowJar
