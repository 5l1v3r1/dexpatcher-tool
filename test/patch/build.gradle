/*
 * DexPatcher - Copyright 2015, 2016 Rodrigo Balerdi
 * (GNU General Public License version 3 or later)
 *
 * DexPatcher is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published
 * by the Free Software Foundation, either version 3 of the License,
 * or (at your option) any later version.
 */

plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "1.2.3"
}

dependencies {
    compile project(':annotation')
}

jar.enabled = false

shadowJar {
    dependsOn jar
    classifier = null
    from tasks.getByPath(':annotation:jar')
}
assemble.dependsOn shadowJar

task dex(type:Exec) {

    dependsOn shadowJar
    ext.inputFile = shadowJar.archivePath
    ext.outputFile = file("build/libs/${project.name}.dex")

    inputs.file parent.dxPath
    inputs.file inputFile
    outputs.file outputFile

    executable parent.dxPath
    args '--dex', '--output', outputFile, inputFile

}
