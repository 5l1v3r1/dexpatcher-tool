/*
 * DexPatcher - Copyright 2015-2019 Rodrigo Balerdi
 * (GNU General Public License version 3 or later)
 *
 * DexPatcher is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published
 * by the Free Software Foundation, either version 3 of the License,
 * or (at your option) any later version.
 */

import groovy.transform.CompileStatic

plugins {
    id 'java-base'
}

// This project implements two test tasks:
// 1) The portable test, that is reduced in scope:
//    -Produces a patched dex file from test source and patch files.
// 2) The shell test, that is Linux-only:
//    -Produces a patched dex file from test source and patch files.
//    -Runs the source and patched dex files.
//    -Captures all output and compares it with a reference output.

// Select tests to run when executing the 'check' task:
def runPortableTest = false
def runShellTest = false

def dexpatcherJar = tasks.getByPath(':tool:shadowJar').archiveFile

def sourceJar = tasks.getByPath(':test:source:jar').archiveFile
def patchJar = tasks.getByPath(':test:patch:jar').archiveFile

// Portable Test

repositories {
    google()
}

configurations {
    r8
}

dependencies {
    r8 'com.android.tools:r8:1.5.68'
}

@CompileStatic
@CacheableTask
class DexerTask extends JavaExec {

    @PathSensitive(PathSensitivity.RELATIVE)
    @InputFiles final ConfigurableFileCollection inputFiles = project.files()
    @OutputFile final RegularFileProperty outputFile = project.objects.fileProperty()

    @Input final ListProperty<String> extraArgs = project.objects.listProperty(String)

    DexerTask() {

        group = 'verification'
        classpath project.configurations.getByName('r8')
        main = 'com.android.tools.r8.D8'

    }

    @Override void exec() {

        def args = new ArrayList<String>()

        args.add('--no-desugaring')
        args.addAll(['--output', outputFile.get() as String])

        args.addAll(extraArgs.get())
        //args.add('--')    // not supported by d8

        if (inputFiles.empty) throw new RuntimeException('No input files specified')
        args.addAll(inputFiles as List<String>)

        super.setArgs args
        super.exec()

    }

}

task sourceDex(type:DexerTask) {

    inputFiles.setFrom sourceJar
    outputFile.set layout.buildDirectory.file('libs/source.dex.zip')

}

task patchDex(type:DexerTask) {

    inputFiles.setFrom patchJar
    outputFile.set layout.buildDirectory.file('libs/patch.dex.zip')

}

task portableTest(type:JavaExec) {

    group 'verification'

    outputs.upToDateWhen { false }

    ext.sourceFile = sourceDex.outputFile
    ext.patchFile = patchDex.outputFile
    ext.outputFile = objects.fileProperty()
    outputFile.set layout.buildDirectory.file('libs/patched.dex')

    inputs.file sourceFile
    inputs.file patchFile
    outputs.file outputFile

    classpath dexpatcherJar
    args '--verbose', '--output', outputFile.get(), sourceFile.get(), patchFile.get()

}

// Shell Test (Linux-only)

// Tool paths for this test are configured in 'shell-test.config'.

task shellTest(type:Exec) {

    group 'verification'

    inputs.file dexpatcherJar
    inputs.file sourceJar
    inputs.file patchJar
    inputs.file file('shell-test')
    inputs.file file('shell-test.config')
    inputs.file file('shell-test-diff')
    inputs.file file('shell-test-ref.txt')
    outputs.file file('build/shell-test-out.txt')

    commandLine 'bash', 'shell-test-diff'

}

// This task sets the shell test output reference to the current output.

task shellTestSetReference(type:Exec) {

    group 'verification'

    dependsOn dexpatcherJar, sourceJar, patchJar

    commandLine 'bash', 'shell-test-set-ref'

}

// Test Selection

check {
    if (runPortableTest) dependsOn portableTest
    if (runShellTest) dependsOn shellTest
}
